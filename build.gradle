import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'application'
}

group 'org.freerails'
version '0.4.1'

java {
    sourceCompatibility JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

sourceSets {
    tools
}

dependencies {
    implementation 'ch.qos.reload4j:reload4j:1.2.26' // replacement of log4j:log4j:1.2.17
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'commons-io:commons-io:2.20.0'
    implementation 'org.jetbrains:annotations:26.0.2'
    testImplementation 'junit:junit:4.13.2'
    toolsImplementation sourceSets.main.output
}

mainClassName = 'freerails.client.launcher.LauncherFrame'

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

configurations.implementation.setCanBeResolved(true)

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes(
                 'Main-Class': mainClassName
        )
    }
    from {
        configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task generateVersionInfo {
    // gets the short SHA of the last commit and the information if there are uncommitted changes (-M indicating that there is a modification)
    String git_revision = "git rev-parse --short HEAD".execute().text.trim() + ("git diff-index --quiet HEAD --".execute().waitFor() == 1 ? "-M" : "")
    // get current date and time in a fixed format
    SimpleDateFormat sdfDate = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss")
    String date = sdfDate.format(new Date())

    def outputDir = file("$buildDir/../src/main/java/freerails")
    outputs.dir outputDir
    doFirst {
        outputDir.exists() || outputDir.mkdirs()
        def srcFile = new File(outputDir, "Version.java")
        srcFile.write(
                """
package freerails;

public final class Version {
    public static final String VERSION = "$project.version";
    public static final String GIT_REVISION = "$git_revision";
    public static final String CREATION_DATE = "$date";
}
""")
    }
}

compileJava.dependsOn generateVersionInfo
compileJava.source generateVersionInfo.outputs.files, sourceSets.main.java